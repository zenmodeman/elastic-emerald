raw `

.set LOCALID_STAIR_GUARD, 2

RustboroCity_DevonCorp_1F_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, RustboroCity_DevonCorp_1F_OnTransition
	.byte 0

RustboroCity_DevonCorp_1F_OnTransition:
	call_if_unset FLAG_RETURNED_DEVON_GOODS, RustboroCity_DevonCorp_1F_EventScript_BlockStairs
	end

RustboroCity_DevonCorp_1F_EventScript_BlockStairs::
	setobjectxyperm LOCALID_STAIR_GUARD, 14, 2
	setobjectmovementtype LOCALID_STAIR_GUARD, MOVEMENT_TYPE_FACE_DOWN
	return

RustboroCity_DevonCorp_1F_EventScript_Employee::
	lock
	faceplayer
	goto_if_set FLAG_RETURNED_DEVON_GOODS, RustboroCity_DevonCorp_1F_EventScript_GoodsRecovered
	goto_if_set FLAG_DEVON_GOODS_STOLEN, RustboroCity_DevonCorp_1F_EventScript_RobberWasntBright
	msgbox RustboroCity_DevonCorp_1F_Text_ThoseShoesAreOurProduct, MSGBOX_DEFAULT
	release
	end

RustboroCity_DevonCorp_1F_EventScript_RobberWasntBright::
	msgbox RustboroCity_DevonCorp_1F_Text_RobberWasntVeryBright, MSGBOX_DEFAULT
	release
	end

RustboroCity_DevonCorp_1F_EventScript_GoodsRecovered::
	msgbox RustboroCity_DevonCorp_1F_Text_SoundsLikeStolenGoodsRecovered, MSGBOX_DEFAULT
	release
	end

RustboroCity_DevonCorp_1F_EventScript_StairGuard::
	lock
	faceplayer
	goto_if_set FLAG_RETURNED_DEVON_GOODS, RustboroCity_DevonCorp_1F_EventScript_AlwaysWelcome
	goto_if_set FLAG_RECOVERED_DEVON_GOODS, RustboroCity_DevonCorp_1F_EventScript_GotRobbed
	goto_if_set FLAG_DEVON_GOODS_STOLEN, RustboroCity_DevonCorp_1F_EventScript_GotRobbed
	msgbox RustboroCity_DevonCorp_1F_Text_OnlyAuthorizedPeopleEnter, MSGBOX_DEFAULT
	release
	end

RustboroCity_DevonCorp_1F_EventScript_AlwaysWelcome::
	msgbox RustboroCity_DevonCorp_1F_Text_YoureAlwaysWelcomeHere, MSGBOX_DEFAULT
	release
	end

RustboroCity_DevonCorp_1F_EventScript_GotRobbed::
	msgbox RustboroCity_DevonCorp_1F_Text_HowCouldWeGetRobbed, MSGBOX_DEFAULT
	release
	end

RustboroCity_DevonCorp_1F_EventScript_Greeter::
	lock
	faceplayer
	goto_if_set FLAG_RETURNED_DEVON_GOODS, RustboroCity_DevonCorp_1F_EventScript_WelcomeToDevonCorp
	goto_if_set FLAG_RECOVERED_DEVON_GOODS, RustboroCity_DevonCorp_1F_EventScript_StaffGotRobbed
	goto_if_set FLAG_DEVON_GOODS_STOLEN, RustboroCity_DevonCorp_1F_EventScript_StaffGotRobbed
	msgbox RustboroCity_DevonCorp_1F_Text_WelcomeToDevonCorp, MSGBOX_DEFAULT
	release
	end

RustboroCity_DevonCorp_1F_EventScript_WelcomeToDevonCorp::
	msgbox RustboroCity_DevonCorp_1F_Text_WelcomeToDevonCorp, MSGBOX_DEFAULT
	release
	end

RustboroCity_DevonCorp_1F_EventScript_StaffGotRobbed::
	msgbox RustboroCity_DevonCorp_1F_Text_StaffGotRobbed, MSGBOX_DEFAULT
	release
	end

RustboroCity_DevonCorp_1F_EventScript_RocksMetalDisplay::
	msgbox RustboroCity_DevonCorp_1F_Text_RocksMetalDisplay, MSGBOX_SIGN
	end

RustboroCity_DevonCorp_1F_EventScript_ProductsDisplay::
	msgbox RustboroCity_DevonCorp_1F_Text_ProductDisplay, MSGBOX_SIGN
	end

RustboroCity_DevonCorp_1F_Text_WelcomeToDevonCorp:
	.string "Hello and welcome to the DEVON\n"
	.string "CORPORATION.\p"
	.string "We're proud producers of items and\n"
	.string "medicine that enhance your life.$"

RustboroCity_DevonCorp_1F_Text_StaffGotRobbed:
	.string "One of our research staff stupidly\n"
	.string "got robbed of an important parcel.$"

RustboroCity_DevonCorp_1F_Text_ThoseShoesAreOurProduct:
	.string "Hey, those RUNNING SHOES!\n"
	.string "They're one of our products!\p"
	.string "It makes me happy when I see someone\n"
	.string "using something we made.$"

RustboroCity_DevonCorp_1F_Text_RobberWasntVeryBright:
	.string "That stolen parcel…\p"
	.string "Well, sure it's important, but it's not\n"
	.string "anything that anyone can use.\p"
	.string "In my estimation, that robber must not\n"
	.string "have been very bright.$"

RustboroCity_DevonCorp_1F_Text_SoundsLikeStolenGoodsRecovered:
	.string "It sounds like they've recovered\n"
	.string "the ripped-off DEVON GOODS.$"

RustboroCity_DevonCorp_1F_Text_OnlyAuthorizedPeopleEnter:
	.string "I'm sorry, only authorized people\n"
	.string "are allowed to enter here.$"

RustboroCity_DevonCorp_1F_Text_HowCouldWeGetRobbed:
	.string "It's beyond stupid.\n"
	.string "How could we get robbed?$"

RustboroCity_DevonCorp_1F_Text_YoureAlwaysWelcomeHere:
	.string "Hi, there!\n"
	.string "You're always welcome here!$"

RustboroCity_DevonCorp_1F_Text_RocksMetalDisplay:
	.string "Samples of rocks and metal are\n"
	.string "displayed in the glass case.\p"
	.string "There's a panel with some writing\n"
	.string "on it…\p"
	.string "“DEVON CORPORATION got its start as\n"
	.string "a producer of stones from quarries.\p"
	.string "“The company also produced iron from\n"
	.string "filings in the sand.\p"
	.string "“From that humble start as a producer\n"
	.string "of raw materials, DEVON developed.\p"
	.string "“DEVON is now a manufacturer of a wide\n"
	.string "range of industrial products.”$"

RustboroCity_DevonCorp_1F_Text_ProductDisplay:
	.string "Prototypes and test products fill\n"
	.string "the glass display case.\p"
	.string "There's a panel with a description…\p"
	.string "“In addition to industrial products,\n"
	.string "DEVON now markets sundries and\l"
	.string "pharmaceuticals for better lifestyles.\p"
	.string "“Recently, DEVON has begun marketing\n"
	.string "tools for POKéMON TRAINERS, including\l"
	.string "POKé BALLS and POKéNAV systems.”$"


`

script RustboroCity_DevonCorp_1F_EventScript_IVTrainer{
    lock
    msgbox(format("Greetings, I am versed in Hyper Training Pokemon. If you have any bottle capsules, I can help you train your pokemon."))
    msgbox(format("What capsules would you be willing to offer?"))  

	checkitem(ITEM_PLAIN_BOTTLE_CAP)
	if (var(VAR_RESULT) == TRUE){
		dynmultipush("Plain Capsules", ITEM_PLAIN_BOTTLE_CAP)
	}
    checkitem(ITEM_BOTTLE_CAP)
    if (var(VAR_RESULT) == TRUE){
        dynmultipush("Regular Capsules", ITEM_BOTTLE_CAP)
    }
    checkitem(ITEM_GOLD_BOTTLE_CAP)
        if (var(VAR_RESULT) == TRUE){
        dynmultipush("Gold Capsules", ITEM_GOLD_BOTTLE_CAP)
    }
	dynmultipush("Never Mind", ITEM_NONE)
	dynmultistack(0, 0, FALSE, 6, FALSE, ITEM_NONE, DYN_MULTICHOICE_CB_NONE)
	if (var(VAR_RESULT) == ITEM_NONE || var(VAR_RESULT) ==  MULTI_B_PRESSED){
		goto(RustboroCity_DevonCorp_1F_IVTrainer_Farewell)
	}
	if (var(VAR_RESULT) == ITEM_PLAIN_BOTTLE_CAP){
		msgbox(format("For this capsule, I can give you moderate training in one stat."))
		waitmessage
		call(IVTrainer_ChooseStat)
		call(IVTrainer_ChooseMon)
		specialvar(VAR_RESULT, TrainPlainIV)
		if (var(VAR_RESULT) == FALSE){
			msgbox(format("This Pokemon already exceeds my training capacities for this stat! Please come again with other training."))
			waitmessage
			releaseall
			end
		}
		playfanfare(MUS_LEVEL_UP)
		waitfanfare
		removeitem(ITEM_PLAIN_BOTTLE_CAP, 1)
		msgbox(format("It's done. Your Pokémon looks much stronger now."))
		waitmessage
	}elif (var(VAR_RESULT) == ITEM_BOTTLE_CAP){
		msgbox(format("For this capsule, I can maximize one stat."))
		waitmessage
		call(IVTrainer_ChooseStat)
		call(IVTrainer_ChooseMon)
		specialvar(VAR_RESULT, TrainMaxIV)
		if (var(VAR_RESULT) == FALSE){
			msgbox(format("This Pokemon already exceeds my training capacities for this stat! Please come again with other training."))
			waitmessage
			releaseall
			end
		}
		playfanfare(MUS_LEVEL_UP)
		waitfanfare
		removeitem(ITEM_BOTTLE_CAP, 1)
		msgbox(format("It's done. Your Pokémon looks much stronger now."))
		waitmessage
	}elif (var(VAR_RESULT) == ITEM_GOLD_BOTTLE_CAP){
		msgbox(format("For this capsule, I can maximize all your pokemon's stats."))
		waitmessage
		call(IVTrainer_ChooseMon)
		specialvar(VAR_RESULT, TrainMaxIVs)
		if (var(VAR_RESULT) == FALSE){
			msgbox(format("This Pokemon already exceeds my training capacities for these stats! Please come again with other training."))
			waitmessage
			releaseall
			end
		}
		playfanfare(MUS_LEVEL_UP)
		waitfanfare
		removeitem(ITEM_GOLD_BOTTLE_CAP, 1)
		msgbox(format("It's done. Your Pokémon looks much stronger now."))
		waitmessage	
	}
	releaseall
	end
}

script IVTrainer_ChooseStat{
	msgbox(format("Which stat should I train?"))
	dynmultichoice(20, 100, FALSE, 5, 0, DYN_MULTICHOICE_CB_NONE, "HP", "Atk", "Def", "Speed", "Sp.atk", "Sp.def", "Never Mind")
	if (var(VAR_RESULT) == 6 || var(VAR_RESULT) == MULTI_B_PRESSED){
		goto(RustboroCity_DevonCorp_1F_IVTrainer_Farewell)
	}
	copyvar(VAR_TEMP_1, VAR_RESULT)
}
script RustboroCity_DevonCorp_1F_IVTrainer_Farewell{
	msgbox("Alright, have a nice day!")
	releaseall
	end
}

script IVTrainer_ChooseMon{
		msgbox(format("Alright, Please choose a Pokemon to train."))
		special(ChoosePartyMon)
		waitstate
		if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
			msgbox(format("Having trouble deciding? Take your time."))
			waitmessage
			releaseall
			end
		}
}