raw`
InsideOfTruck_MapScripts::
	map_script MAP_SCRIPT_ON_LOAD, InsideOfTruck_OnLoad
	map_script MAP_SCRIPT_ON_RESUME, InsideOfTruck_OnResume
	.byte 0

InsideOfTruck_OnLoad:
	setmetatile 4, 1, METATILE_InsideOfTruck_ExitLight_Top, FALSE
	setmetatile 4, 2, METATILE_InsideOfTruck_ExitLight_Mid, FALSE
	setmetatile 4, 3, METATILE_InsideOfTruck_ExitLight_Bottom, FALSE
	end

InsideOfTruck_OnResume:
	setstepcallback STEP_CB_TRUCK
	end


InsideOfTruck_EventScript_SetIntroFlagsMale::
	setrespawn HEAL_LOCATION_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F
	setvar VAR_LITTLEROOT_INTRO_STATE, 1
	setflag FLAG_HIDE_LITTLEROOT_TOWN_MAYS_HOUSE_MOM
	setflag FLAG_HIDE_LITTLEROOT_TOWN_MAYS_HOUSE_TRUCK
	setflag FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_RIVAL_MOM
	setflag FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_RIVAL_SIBLING
	setflag FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F_POKE_BALL
	setvar VAR_LITTLEROOT_HOUSES_STATE_BRENDAN, 1
	setdynamicwarp MAP_LITTLEROOT_TOWN, 3, 10
	releaseall
	end

InsideOfTruck_EventScript_SetIntroFlagsFemale::
	setrespawn HEAL_LOCATION_LITTLEROOT_TOWN_MAYS_HOUSE_2F
	setvar VAR_LITTLEROOT_INTRO_STATE, 2
	setflag FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_MOM
	setflag FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_TRUCK
	setflag FLAG_HIDE_LITTLEROOT_TOWN_MAYS_HOUSE_RIVAL_MOM
	setflag FLAG_HIDE_LITTLEROOT_TOWN_MAYS_HOUSE_RIVAL_SIBLING
	setflag FLAG_HIDE_LITTLEROOT_TOWN_MAYS_HOUSE_2F_POKE_BALL
	setvar VAR_LITTLEROOT_HOUSES_STATE_MAY, 1
	setdynamicwarp MAP_LITTLEROOT_TOWN, 12, 10
	releaseall
	end

InsideOfTruck_EventScript_MovingBox::
	msgbox InsideOfTruck_Text_BoxPrintedWithMonLogo, MSGBOX_SIGN
	end

InsideOfTruck_Text_BoxPrintedWithMonLogo:
	.string "The box is printed with a POKéMON logo.\p"
	.string "It's a POKéMON brand moving and\n"
	.string "delivery service.$"

`


script InsideOfTruck_EventScript_SetIntroFlags{
    lockall
    setflag(FLAG_HIDE_MAP_NAME_POPUP)
    special(EnableNationalPokedex)
    msgbox(format("Before begginning your game, let's configure some settings."))
    do{
        msgbox(format("Will you opt-in to Restricted Mode?"))
        dynmultichoice(7, 100, TRUE, 3, 2, DYN_MULTICHOICE_CB_NONE, "Yes", "No", "Info")
        if (var(VAR_RESULT) == 0){
            setflag(FLAG_RESTRICTED_MODE)
        }
        elif (var(VAR_RESULT) ==2){
            msgbox(format("Restricted mode adds constraints for balance reasons."))
            msgbox(format("Certain moves, items, and abilities are less available."))
            msgbox(format("Bag items can't be used during Trainer Battles."))
            msgbox(format("Item clause is forced during Gym Battles and the Elite Four."))
        }
    }while (var(VAR_RESULT) ==2)

    
    do{
        msgbox("Will you opt-in to Resource Mode?")
        dynmultichoice(7, 100, TRUE, 3, 2, DYN_MULTICHOICE_CB_NONE, "Yes", "No", "Info")
        if (var(VAR_RESULT) == 0){
            setflag(FLAG_RESOURCE_MODE)
        }
        elif (var(VAR_RESULT) ==2){
            msgbox(format("Resource Mode emphasizes finite supply to enforce more decision-making."))
            msgbox(format("TMs, tutors, and move relearners have limited use."))
            msgbox(format("Consumable items do not regenerate after battle, and berries cannot be planted."))
            msgbox(format("Choosing to not play with Resource Mode grants much more flexibility."))
        }
    }while (var(VAR_RESULT) ==2)
    
    
    do{
        msgbox("Will you do a monotype run?")
        dynmultichoice(7, 100, TRUE, 3, 2, DYN_MULTICHOICE_CB_NONE, "Yes", "No", "Info")
        if (var(VAR_RESULT) ==2){
            msgbox(format("For monotype runs, each monotype has custom wild encounters and gifts."))
            msgbox(format("Split Evolutions are constrained: a Wurmple in Mono Poison will always evolve into a Cascoon."))    
        }
    }while (var(VAR_RESULT) ==2)

    if (var(VAR_RESULT) == 0){
        msgbox("Please choose the monotype.") 
        dynmultichoice(5, 100, TRUE, 4, 0, DYN_MULTICHOICE_CB_NONE, "Normal", "Fighting", "Flying", "Poison", "Ground", "Rock", "Bug", "Ghost", "Steel", "Fire", "Water", "Grass", "Electric", "Psychic", "Ice", "Dragon", "Dark", "Fairy")
        copyvar(VAR_MONOTYPE, VAR_RESULT)
        addvar(VAR_MONOTYPE, 1)
    }

    checkplayergender 
    if (var(VAR_RESULT) == MALE){
        goto(InsideOfTruck_EventScript_SetIntroFlagsMale)
    }elif(var(VAR_RESULT) == FEMALE){
        goto(InsideOfTruck_EventScript_SetIntroFlagsFemale)
    }
    releaseall
}