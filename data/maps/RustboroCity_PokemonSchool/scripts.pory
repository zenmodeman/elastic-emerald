raw`
.set LOCALID_RIVAL, 7
.set LOCALID_TEACHER, 5
.set LOCALID_ROXANNE, 8

RustboroCity_PokemonSchool_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, TrainerSchoolTransition
	.byte 0

RustboroCity_PokemonSchool_EventScript_Blackboard::
	lockall
	msgbox RustboroCity_PokemonSchool_Text_BlackboardListsStatusChanges, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic::
	message RustboroCity_PokemonSchool_Text_ReadWhichTopic
	waitmessage
	multichoicegrid 8, 1, MULTI_STATUS_INFO, 3, FALSE
	switch VAR_RESULT
	case 0, RustboroCity_PokemonSchool_EventScript_Poison
	case 1, RustboroCity_PokemonSchool_EventScript_Paralysis
	case 2, RustboroCity_PokemonSchool_EventScript_Sleep
	case 3, RustboroCity_PokemonSchool_EventScript_Burn
	case 4, RustboroCity_PokemonSchool_EventScript_Freeze
	case 5, RustboroCity_PokemonSchool_EventScript_ExitTopicSelect
	case MULTI_B_PRESSED, RustboroCity_PokemonSchool_EventScript_ExitTopicSelect
	end

RustboroCity_PokemonSchool_EventScript_Poison::
	msgbox RustboroCity_PokemonSchool_Text_ExplainPoison, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_Paralysis::
	msgbox RustboroCity_PokemonSchool_Text_ExplainParalysis, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_Sleep::
	msgbox RustboroCity_PokemonSchool_Text_ExplainSleep, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_Burn::
	msgbox RustboroCity_PokemonSchool_Text_ExplainBurn, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_Freeze::
	msgbox RustboroCity_PokemonSchool_Text_ExplainFreeze, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_ChooseBlackboardTopic
	end

RustboroCity_PokemonSchool_EventScript_ExitTopicSelect::
	releaseall
	end

RustboroCity_PokemonSchool_EventScript_GameboyKid1::
	msgbox RustboroCity_PokemonSchool_Text_TradingRightNow, MSGBOX_NPC
	end

RustboroCity_PokemonSchool_EventScript_GameboyKid2::
	msgbox RustboroCity_PokemonSchool_Text_AlwaysWantedSeedot, MSGBOX_NPC
	end

RustboroCity_PokemonSchool_EventScript_RichBoy::
	msgbox RustboroCity_PokemonSchool_Text_PokemontCantUseManMadeItems, MSGBOX_NPC
	end

RustboroCity_PokemonSchool_EventScript_Lass::
	msgbox RustboroCity_PokemonSchool_Text_ConfusedPokemonAttacksItself, MSGBOX_NPC
	end

RustboroCity_PokemonSchool_EventScript_SchoolKidM::
	msgbox RustboroCity_PokemonSchool_Text_PokemonHealItselfWithBerry, MSGBOX_NPC
	end

RustboroCity_PokemonSchool_EventScript_StudentNotebook::
	msgbox RustboroCity_PokemonSchool_Text_StudentsNotes, MSGBOX_SIGN
	end

RustboroCity_PokemonSchool_EventScript_Teacher::
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_QUICK_CLAW, RustboroCity_PokemonSchool_EventScript_GaveQuickClaw
	call_if_eq VAR_FACING, DIR_EAST, RustboroCity_PokemonSchool_EventScript_TeacherCheckOnStudentsEast
	call_if_eq VAR_FACING, DIR_WEST, RustboroCity_PokemonSchool_EventScript_TeacherCheckOnStudentsWest
	msgbox RustboroCity_PokemonSchool_Text_StudentsWhoDontStudyGetQuickClaw, MSGBOX_DEFAULT
	giveitem ITEM_QUICK_CLAW
	goto_if_eq VAR_RESULT, 0, Common_EventScript_ShowBagIsFull
	closemessage
	applymovement VAR_LAST_TALKED, Common_Movement_WalkInPlaceFasterDown
	waitmovement 0
	setflag FLAG_RECEIVED_QUICK_CLAW
	release
	end

RustboroCity_PokemonSchool_EventScript_TeacherCheckOnStudentsEast::
	applymovement VAR_LAST_TALKED, RustboroCity_PokemonSchool_Movement_TeacherCheckOnStudentsEast
	waitmovement 0
	return

RustboroCity_PokemonSchool_EventScript_TeacherCheckOnStudentsWest::
	applymovement VAR_LAST_TALKED, RustboroCity_PokemonSchool_Movement_TeacherCheckOnStudentsWest
	waitmovement 0
	return

RustboroCity_PokemonSchool_EventScript_GaveQuickClaw::
	msgbox RustboroCity_PokemonSchool_Text_ExplainQuickClaw, MSGBOX_DEFAULT
	closemessage
	applymovement VAR_LAST_TALKED, Common_Movement_WalkInPlaceFasterDown
	waitmovement 0
	release
	end

RustboroCity_PokemonSchool_Movement_TeacherCheckOnStudentsWest:
	walk_left
	walk_down
	walk_down
	walk_right
	walk_in_place_faster_down
	delay_16
	delay_16
	delay_16
	walk_down
	walk_in_place_faster_left
	delay_16
	delay_16
	walk_right
	delay_16
	delay_16
	delay_8
	walk_up
	walk_left
	walk_left
	walk_up
	walk_up
	walk_right
	step_end

RustboroCity_PokemonSchool_Movement_TeacherCheckOnStudentsEast:
	walk_right
	walk_right
	walk_down
	walk_down
	walk_left
	walk_left
	walk_in_place_faster_down
	delay_16
	delay_16
	delay_16
	walk_down
	walk_in_place_faster_left
	delay_16
	delay_16
	walk_right
	delay_16
	delay_16
	delay_8
	walk_up
	walk_right
	walk_up
	walk_up
	walk_left
	walk_left
	step_end

RustboroCity_PokemonSchool_EventScript_Scott::
	lock
	faceplayer
	goto_if_set FLAG_MET_SCOTT_AFTER_OBTAINING_STONE_BADGE, RustboroCity_PokemonSchool_EventScript_ScottWatchStudents
	goto_if_set FLAG_MET_SCOTT_RUSTBORO, RustboroCity_PokemonSchool_EventScript_ScottSpokeAlready
	goto_if_set FLAG_BADGE01_GET, RustboroCity_PokemonSchool_EventScript_ScottGreetHasBadge
	msgbox RustboroCity_PokemonSchool_Text_ScottMetAlreadyCut, MSGBOX_DEFAULT
	addvar VAR_SCOTT_STATE, 1
	setflag FLAG_MET_SCOTT_RUSTBORO
	release
	end

RustboroCity_PokemonSchool_EventScript_ScottSpokeAlready::
	goto_if_set FLAG_BADGE01_GET, RustboroCity_PokemonSchool_EventScript_ScottNoticeBadge
	msgbox RustboroCity_PokemonSchool_Text_StudentTalentLevelUnknown, MSGBOX_DEFAULT
	release
	end

RustboroCity_PokemonSchool_EventScript_ScottGreetHasBadge::
	msgbox RustboroCity_PokemonSchool_Text_ScottMetAlreadyStoneBadge, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_MetScottAfterBadge
	end

RustboroCity_PokemonSchool_EventScript_ScottNoticeBadge::
	msgbox RustboroCity_PokemonSchool_Text_ScottStoneBadge, MSGBOX_DEFAULT
	goto RustboroCity_PokemonSchool_EventScript_MetScottAfterBadge
	end

RustboroCity_PokemonSchool_EventScript_MetScottAfterBadge::
	addvar VAR_SCOTT_STATE, 1
	setflag FLAG_MET_SCOTT_AFTER_OBTAINING_STONE_BADGE
	release
	end

RustboroCity_PokemonSchool_EventScript_ScottWatchStudents::
	msgbox RustboroCity_PokemonSchool_Text_StudentTalentLevelUnknown, MSGBOX_DEFAULT
	release
	end

RustboroCity_PokemonSchool_Text_BlackboardListsStatusChanges:
	.string "The blackboard lists status changes\n"
	.string "that may affect POKéMON in battle.$"

RustboroCity_PokemonSchool_Text_ReadWhichTopic:
	.string "Which topic do you want to read?$"

RustboroCity_PokemonSchool_Text_ExplainPoison:
	.string "If a POKéMON is poisoned, it will\n"
	.string "steadily lose HP.\p"
	.string "The effects of poison remain after\n"
	.string "a battle.\p"
	.string "A poisoned POKéMON's HP will drop\n"
	.string "while it is traveling.\p"
	.string "Heal a poisoning using an ANTIDOTE.$"

RustboroCity_PokemonSchool_Text_ExplainParalysis:
	.string "If a POKéMON becomes paralyzed,\n"
	.string "its SPEED drops.\p"
	.string "It may also not be able to move while\n"
	.string "it is in battle.\p"
	.string "Paralysis remains after battle.\n"
	.string "Cure it using a PARLYZ HEAL.$"

RustboroCity_PokemonSchool_Text_ExplainSleep:
	.string "If a POKéMON falls asleep, it will be\n"
	.string "unable to attack.\p"
	.string "A POKéMON may wake up on its own,\n"
	.string "but if a battle ends while it is\l"
	.string "sleeping, it will stay asleep.\p"
	.string "Wake it up using an AWAKENING.$"

RustboroCity_PokemonSchool_Text_ExplainBurn:
	.string "A burn reduces ATTACK power, and it\n"
	.string "steadily reduces the victim's HP.\p"
	.string "A burn lingers after battle.\n"
	.string "Cure a burn using a BURN HEAL.$"

RustboroCity_PokemonSchool_Text_ExplainFreeze:
	.string "If a POKéMON is frozen, it becomes\n"
	.string "completely helpless.\p"
	.string "It will remain frozen after battle.\n"
	.string "Thaw it out using an ICE HEAL.$"

RustboroCity_PokemonSchool_Text_StudentsWhoDontStudyGetQuickClaw:
	.string "Students who don't study get a little\n"
	.string "taste of my QUICK CLAW.\p"
	.string "Whether or not you are a good student \n"
	.string "will be evident from the way you use\l"
	.string "this item.$"

RustboroCity_PokemonSchool_Text_ExplainQuickClaw:
	.string "A POKéMON holding the QUICK CLAW will\n"
	.string "occasionally speed up and get to move\l"
	.string "before its opponent.\p"
	.string "There are many other items that are\n"
	.string "meant to be held by POKéMON.\p"
	.string "Just those alone will give you many\n"
	.string "topics to study!$"

RustboroCity_PokemonSchool_Text_TradingRightNow:
	.string "I'm trading POKéMON with my friend\n"
	.string "right now.$"

RustboroCity_PokemonSchool_Text_AlwaysWantedSeedot:
	.string "I always wanted a SEEDOT, and\n"
	.string "I'm finally getting one!$"

RustboroCity_PokemonSchool_Text_PokemontCantUseManMadeItems:
	.string "POKéMON can hold items, but they\n"
	.string "don't know what to do with man-made\l"
	.string "items like POTION and ANTIDOTE.$"

RustboroCity_PokemonSchool_Text_ConfusedPokemonAttacksItself:
	.string "You know how some POKéMON moves can\n"
	.string "confuse a POKéMON?\p"
	.string "A confused POKéMON will sometimes\n"
	.string "attack itself without meaning to.\p"
	.string "But once it leaves battle, it will\n"
	.string "return to normal.$"

RustboroCity_PokemonSchool_Text_PokemonHealItselfWithBerry:
	.string "A POKéMON holding a BERRY will heal\n"
	.string "itself…\p"
	.string "There are many kinds of items that\n"
	.string "POKéMON can hold…\p"
	.string "Boy, it sure is hard taking notes\n"
	.string "down…$"

RustboroCity_PokemonSchool_Text_StudentsNotes:
	.string "It's this student's notebook…\p"
	.string "POKéMON are to be caught using\n"
	.string "POKé BALLS.\p"
	.string "Up to six POKéMON can accompany\n"
	.string "a TRAINER.\p"
	.string "A TRAINER is someone who catches\n"
	.string "POKéMON, raises them, and battles\l"
	.string "with them.\p"
	.string "A TRAINER's mission is to defeat\n"
	.string "the strong TRAINERS who await\l"
	.string "challengers in POKéMON GYMS.$"

RustboroCity_PokemonSchool_Text_ScottMetAlreadyCut:
	.string "Hello? Didn't we meet before?\n"
	.string "I think back in PETALBURG CITY.\p"
	.string "Let me introduce myself.\n"
	.string "My name's SCOTT.\p"
	.string "I've been traveling everywhere in\n"
	.string "search of outstanding TRAINERS.\p"
	.string "More specifically, I'm looking for\n"
	.string "POKéMON battle experts.\p"
	.string "So, what brings you to this SCHOOL?\n"
	.string "Are you a TRAINER, too?\p"
	.string "The first thing you should do is to\n"
	.string "have a POKéMON learn the move CUT.\p"
	.string "If I remember correctly, someone in\n"
	.string "this town has CUT.$"

RustboroCity_PokemonSchool_Text_StudentTalentLevelUnknown:
	.string "SCOTT: Hmm…\p"
	.string "The talent levels of the students here\n"
	.string "are unknown. The potential's there.$"

RustboroCity_PokemonSchool_Text_ScottStoneBadge:
	.string "SCOTT: Oh, what's that?\p"
	.string "It's a STONE BADGE, isn't it?\n"
	.string "That's pretty impressive, I'd say.\p"
	.string "But, you know, I would've loved to see\n"
	.string "you in battle.\p"
	.string "It's hard to tell what you're like as\n"
	.string "a TRAINER from a LEAGUE BADGE.$"

RustboroCity_PokemonSchool_Text_ScottMetAlreadyStoneBadge:
	.string "Hello? Didn't we meet before?\n"
	.string "I think back in PETALBURG CITY.\p"
	.string "Let me introduce myself.\n"
	.string "My name's SCOTT.\p"
	.string "I've been traveling everywhere in\n"
	.string "search of outstanding TRAINERS.\p"
	.string "More specifically, I'm looking for\n"
	.string "POKéMON battle experts.\p"
	.string "Oh, what's that?\n"
	.string "It's a STONE BADGE, isn't it?\p"
	.string "That's pretty impressive, I'd say, for\n"
	.string "a starting TRAINER like yourself.\p"
	.string "But, you know, I would've loved to see\n"
	.string "you in battle.\p"
	.string "It's hard to tell what you're like as\n"
	.string "a TRAINER from a LEAGUE BADGE.$"
`

script TrainerSchoolTransition{
	call(Common_EventScript_SetupRivalGfxId)
}


script TrainerSchool_Lecture{
	call(TrainerSchool_TeacherIntro)
	call(TrainerSchool_RoxanneSpeech)
	call(TrainerSchool_RivalInterjection)
	call(TrainerSchool_RoxanneReply)
	call(TrainerSchool_RivalReadyForRoxanneBattle)
	call(TrainerSchool_TeacherFinalRemarks)
}



script TrainerSchool_TeacherIntro{
	applymovement(LOCALID_TEACHER, TeacherLecture_MovesTowardsClass)
	waitmovement(LOCALID_TEACHER)
	msgbox(format("Good morning, class! Today, we have a special guest joining us - Roxanne!"))
	msgbox(format("You know her as our city's Gym Leader, but some of you remember her as one of our star pupils!"))
	msgbox(format("She's here to share her insider perspective on today's topic."))
	applymovement(LOCALID_TEACHER, TeacherLecture_Right_And_Face_Down)
	waitmovement(LOCALID_TEACHER)
	msgbox(format("Our lesson today is about the Terastal phenomenon, first discovered in the Paldea region."))
	msgbox(format("Thanks to advances in Tera Orbs, trainers worldwide can now use this incredible technique."))
	msgbox(format("While Tera Orbs are still pricey to produce, our own Devon Corp is working hard to make them available to more trainers!"))
	applymovement(LOCALID_TEACHER, TeacherLecture_Left_And_Face_Down)
	waitmovement(LOCALID_TEACHER)
	msgbox(format("Terastalization has revolutionized Pokemon battles."))
	msgbox(format("It opens up new strategies and brings out unexpected strengths in every Pokemon."))
	msgbox(format("Naturally, Hoenn's Gym Leaders - our region's Type Experts,"))
	msgbox(format("were among the first to receive Tera Orbs."))
	msgbox(format("And now, let's hear from Roxanne, who'll share what Terastalization means to her!"))
	applymovement(LOCALID_TEACHER, TeacherLecture_MovesAwayFromClass)
	waitmovement(LOCALID_TEACHER)
}

script TrainerSchool_RoxanneSpeech{
	applymovement(LOCALID_ROXANNE, RoxanneLecture_MoveTowardsClass)
	waitmovement(LOCALID_ROXANNE)
	msgbox(format("Thank you for having me here today. It's an honor to speak with the next generation of Hoenn's finest trainers."))
	msgbox(format("Terastalization is a fascinating phenomenon, one that initially challenged my perspective."))
	msgbox(format("As many of you know, I have a deep admiration for Rocks and Rock-type Pokemon."))
	msgbox(format("To me, Rock Pokemon represent resilience, stability, and timeless beauty. Their legacy is as enduring as the earth itself."))
	msgbox(format("When I was younger, seeing a Geodude use Rock Polish for the first time inspired me to study harder."))
	msgbox(format("When I was learning, it felt as though I was polishing myself into a gem, just like my beloved Rock-types."))
	msgbox(format("Rocks have always held a special place in my heart, their beauty as timeless and awe-inspiring as the stars in the night sky."))
	msgbox(format("So, when I first saw non-Rock Pokemon Terastalizing into Rock-types, I felt conflicted."))
	msgbox(format("I worried it might diminish what made Rock-types special."))
	msgbox(format("But then I met a Runerigus and her daughter - Galarian Yamask - while exploring an ancient ruins."))
	msgbox(format("Runerigus captivated me, with her rune-carved form exuding the same elegance and durability I cherish in Rock Pokemon."))
	msgbox(format("Though she wasn't a Rock-type, she resonated with me in a way that felt deeply familiar."))
	msgbox(format("The first time she Terastalized into a Rock-type, it was breathtaking."))
	msgbox(format("She embodied the rugged beauty and unyielding strength of stone, as if she had been one all along."))
	msgbox(format("To those who, like me, are devoted to one Type, I encourage you to embrace the possibilities Terastalization offers."))
	msgbox(format("Your beloved Type can find new expressions through Terastalization, revealing forms and qualities you may come to cherish just as much."))
	msgbox(format("And to those of you who might challenge my gym in the future - "))
	msgbox(format("My Yamask and Runerigus will shine like the finest gemstones, and I look forward to sharing that brilliance with you!"))
	playse(SE_APPLAUSE)
	waitse
}

script TrainerSchool_RivalInterjection{
	applymovement(LOCALID_RIVAL, Common_Movement_FaceLeft)
	waitmovement(LOCALID_RIVAL)
	msgbox(format("{RIVAL}: Hmm, that's an interesting speech, but I think it'd be nice if we got to see Terastalization in action."))
	msgbox(format("I'd like you have a fight if you wouldn't mind."))
}

script TrainerSchool_RoxanneReply{
	applymovement(LOCALID_ROXANNE, Common_Movement_FaceRight)
	waitmovement(LOCALID_ROXANNE)
	msgbox(format("Roxanne: Ah that boldness, you definitely take after Birch."))
	msgbox(format("Sure, I'm down for a quick one-on-one demonstration. It'd be a good lesson for the class too."))	
}


script TrainerSchool_RivalReadyForRoxanneBattle{
	msgbox(format("{RIVAL}: Great!"))
	applymovement(LOCALID_RIVAL, RivalLecture_MoveTowardsRoxanne)
	waitmovement(LOCALID_RIVAL)
	msgbox(format("I can't wait to see what Tera is like."), MSGBOX_AUTOCLOSE)
	special(SavePlayerParty)
	checkplayergender
	if (var(VAR_RESULT) == MALE){
		ai_vs_ai_battle(TRAINER_MAY_TRAINER_SCHOOL, TRAINER_ROXANNE_4, format("The battle has concluded!"))
	}else{
		ai_vs_ai_battle(TRAINER_BRENDAN_TRAINER_SCHOOL, TRAINER_ROXANNE_4, format("The battle has concluded!"))
	}
	clearflag(FLAG_AI_AI_BATTLE)
	special(LoadPlayerParty)
	msgbox(format("{RIVAL}: Wow, I had the type advantage but I completely lost!"))
	msgbox(format("Roxanne: My Yamask shines brilliantly when she becomes Rock-type!"))
	applymovement(LOCALID_ROXANNE, Common_Movement_FaceDown)
	msgbox(format("Roxanne: Did you see class? When Yamask becomes Rock-type, her move Curse takes a whole new form."))
	msgbox(format("With it, she sharpens both her defense and offense like a Rock-type would!"))
	msgbox(format("{RIVAL}: Fascinating indeed, I can see how Yamask becomes more flexible with Tera. Now I'm excited for the Terastal era!"))
}

script TrainerSchool_TeacherFinalRemarks{
	msgbox(format("Teacher: Well, unexpected as that fight was, it sure made for an interesting learning experience."))
	msgbox(format("Thank you Roxanne and {RIVAL}."))
	msgbox(format("Our time is up, so class is dismissed. I hope to see you all again for the next lesson!"))
	fadescreen(FADE_TO_BLACK)
	setflag(FLAG_HIDE_AFTER_LECTURE)
	setvar(VAR_TRAINER_SCHOOL_STATE, 1)
	removeobject(LOCALID_ROXANNE)
	removeobject(LOCALID_RIVAL)
	removeobject(9)
	removeobject(11)
	removeobject(12)
	removeobject(14)
	fadescreen(FADE_FROM_BLACK)
}

script TrainerSchool_BugCatcher{
	lock
	faceplayer
	msgbox(format("Roxanne's speech was inspiring!"))
	msgbox(format("I can't help but wonder - what Pokemon would be the most fascinating to Terastalize into Bug-types?"))
	release
}

script TrainerSchool_Breeder{
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_TRAINER_SCHOOL_EGG)){
		msgbox(format("Please take good care of the egg!"))
	}else{
		if (var(VAR_MONOTYPE) == TYPE_NONE || var(VAR_MONOTYPE) == TYPE_NORMAL || var(VAR_MONOTYPE) == TYPE_FAIRY - 1 || var(VAR_MONOTYPE) == TYPE_ELECTRIC - 1
		|| var(VAR_MONOTYPE) == TYPE_POISON || var(VAR_MONOTYPE) == TYPE_ICE - 1 || var(VAR_MONOTYPE) == TYPE_PSYCHIC - 1){
			msgbox(format("I love breeding Pokemon, but hatching eggs can be so time-consuming."))
			msgbox(format("I wonder if there are any ways to help hatch them faster."))
			msgbox(format("Do you have any ideas?"))
			dynmultichoice(0, 100, FALSE, 5, 0, DYN_MULTICHOICE_CB_NONE, "Aroma Veil", "Flame Body", "Lucky Chant", "Magma Armor", "No Idea")
			if (var(VAR_RESULT) == 0 || var(VAR_RESULT) == 2){
				msgbox(format("Hmm, I don't think those would help."))
			}elif (var(VAR_RESULT) == 1 || var(VAR_RESULT) == 3){
				msgbox(format("Oh, I think I've vaguely heard of that somewhere before. It might be right!"))
				msgbox(format("As thanks, here, have one of my eggs."))
				call(TrainerSchool_GiveEgg)
				msgbox(format("Please take good care of the egg!"))

			}else{
				msgbox(format("Oh, too bad you weren't able to help..."))
			}
		}else{
			msgbox(format("I love breeding Pokemon, but hatching eggs can be so time-consuming."))
			msgbox(format("If only there was a way to help hatch them faster."))
		}
	}
	release
	end
}

script TrainerSchool_GiveEgg{
	if(var(VAR_MONOTYPE) == TYPE_NONE){
		random(3)
		if(var(VAR_RESULT) == 0){
			giveegg(SPECIES_IGGLYBUFF)
			call(TrainerSchool_ProcessEgg)
		}elif(var(VAR_RESULT) == 1){
			giveegg(SPECIES_TOXEL)
			call(TrainerSchool_ProcessEgg)
		}elif(var(VAR_RESULT) == 2){
			giveegg(SPECIES_SMOOCHUM)
			call(TrainerSchool_ProcessEgg)
		}
	}elif(var(VAR_MONOTYPE) == TYPE_NORMAL || var(VAR_MONOTYPE) == TYPE_FAIRY - 1){
		giveegg(SPECIES_IGGLYBUFF)
		call(TrainerSchool_ProcessEgg)
	}elif(var(VAR_MONOTYPE) == TYPE_ELECTRIC - 1 || var(VAR_MONOTYPE) == TYPE_POISON){
		giveegg(SPECIES_TOXEL)
		call(TrainerSchool_ProcessEgg)
	}elif(var(VAR_MONOTYPE) == TYPE_ICE - 1 || var(VAR_MONOTYPE) == TYPE_PSYCHIC - 1){
		giveegg(SPECIES_SMOOCHUM)
		call(TrainerSchool_ProcessEgg)
	}
}

script TrainerSchool_ProcessEgg{
	if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
		playfanfare(MUS_OBTAIN_ITEM)
		msgbox(format("The egg was added to your party!"))
		setflag(FLAG_RECEIVED_TRAINER_SCHOOL_EGG)
	}elif(var(VAR_RESULT) == MON_GIVEN_TO_PC){
		playfanfare(MUS_OBTAIN_ITEM)
		msgbox(format("The egg was added to your PC!"))
		setflag(FLAG_RECEIVED_TRAINER_SCHOOL_EGG)
	}else{
		msgbox(format("Sorry, you didn't have space for the egg."))
	}
}


movement RivalLecture_MoveTowardsRoxanne{
	walk_left
	walk_up
	walk_up
	walk_left
	walk_left 
	walk_left
	walk_left
	walk_down
	face_left
	step_end
}
movement RoxanneLecture_MoveTowardsClass{
	walk_right
	walk_down
	walk_down
	walk_left
	face_down
	step_end
}

movement TeacherLecture_Right_And_Face_Down{
	walk_right
	face_down
	step_end
}

movement TeacherLecture_Left_And_Face_Down{
	walk_left
	face_down
	step_end
}

movement TeacherLecture_MovesTowardsClass{
	walk_left
	walk_down
	walk_down
	walk_right
	face_down
	step_end
}

movement TeacherLecture_MovesAwayFromClass{
	walk_left
	walk_up
	walk_up
	walk_right
	face_down
	step_end
}
text ClassStartsMessage{
	format("Get seated everybody. Class is in session.")
}
script TrainerSchool_TriggerLeft{
    lockall
    msgbox(ClassStartsMessage)
    applymovement(OBJ_EVENT_ID_PLAYER, LeftTriggerMovement)
    waitmovement(OBJ_EVENT_ID_PLAYER) 
	call(TrainerSchool_Lecture)
    releaseall
    end
}


movement LeftTriggerMovement{
	walk_up
    walk_up
    walk_up
    walk_up
    walk_up
	walk_up
	walk_up
	walk_up
    walk_right
    walk_right
	walk_right
	walk_right
    face_up
	step_end
}

script TrainerSchool_TriggerUp{
	lockall
    msgbox(ClassStartsMessage)
    applymovement(OBJ_EVENT_ID_PLAYER, UpTriggerMovement)
    waitmovement(OBJ_EVENT_ID_PLAYER) // Wait until the movement finishes
	call(TrainerSchool_Lecture)
    releaseall
    end
}

movement UpTriggerMovement{
	walk_up
    walk_up
    walk_up
    walk_up
    walk_up
	walk_up
	walk_up
    walk_right
    walk_right
    walk_right
    face_up
	step_end
}

script TrainerSchool_TriggerUpRight{
	lockall
    msgbox(ClassStartsMessage)
    applymovement(OBJ_EVENT_ID_PLAYER, UpRightTriggerMovement)
    waitmovement(OBJ_EVENT_ID_PLAYER)
	call(TrainerSchool_Lecture)
    releaseall
    end
}

movement UpRightTriggerMovement{
	walk_up
    walk_up
    walk_up
    walk_up
    walk_up
	walk_up
	walk_up
    walk_right
    walk_right
    face_up
	step_end
}


script TrainerSchool_TriggerRight{
    lockall
    face_up 
   msgbox(ClassStartsMessage)
    applymovement(OBJ_EVENT_ID_PLAYER, RightTriggerMovement)
    waitmovement(OBJ_EVENT_ID_PLAYER) 
	call(TrainerSchool_Lecture)
    releaseall
    end
}

movement RightTriggerMovement{
	walk_up
    walk_up
    walk_up
    walk_up
    walk_up
	walk_up
	walk_up
	walk_up
    walk_right
    face_up
	step_end
}

